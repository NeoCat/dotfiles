# ãƒ›ã‚¹ãƒˆåˆ¥è¨­å®šèª­ã¿è¾¼ã¿
[ -r ~/.zshrc_by_host ] && source ~/.zshrc_by_host

###
# ç’°å¢ƒå¤‰æ•°è¨­å®š
###
export PATH=$HOME/bin:$PATH:/usr/sbin
export MANPATH=/usr/local/man:$MANPATH
#export EDITOR=emacs
export LSCOLORS="hxExcxdxBxhghdabagacad"
export LS_COLORS="di=01;33:tw=01;33;41:ow=01;33;45"
export NLSPATH=/usr/share/locale/%N
export FTP_PASSIVE=1
export RSH=ssh
export CVS_RSH=ssh
export REPORTTIME=5
which npm >/dev/null && export NODE_PATH=`npm root -g`

# ãƒ‘ã‚¹é‡è¤‡æ’é™¤
typeset -U path PATH
path=(
    # allow directories only (-/)
    # reject world-writable directories (^W)
    $path(N-/^W)
)

SYSTEM=`uname`
if [ "$SYSTEM" = "CYGWIN_NT-5.1" ]; then
	SYSTEM="Cygwin"
fi

###
# ã‚¨ã‚¤ãƒªã‚¢ã‚¹ã„ã‚ã„ã‚
###
if [ $SYSTEM = Darwin ]; then
	alias l="ls -FGv"
	alias ll="ls -FGlav"
	[ ! -e /usr/sbin/ip ] && function ip() { ifconfig }
elif [ $SYSTEM = Linux ]; then
	alias l="ls -F --color"
	alias ll="ls -Fla --color"
else
	alias l="ls -FG"
	alias ll="ls -FGla"
fi
alias j=jobs
alias c=clear
alias cl="c;l"
alias df="df -h"
alias du="du -h"
alias rm="rm -i"
alias cvs="cvs -q -z7"
alias rmmd="rm (*~|.*~|\#*|.\#*)"
alias jp='export LANG=ja_JP.utf8'
alias java='java -Dfile.encoding=UTF-8'
alias javac='javac -J-Dfile.encoding=UTF-8'
alias egrepr="egrep -rnI --color=auto"
alias rsyncr="rsync -avz --del"
alias hex="noglob printf '0x%x\n'"
alias mv='nocorrect mv'			#ã‚¹ãƒšãƒ«ãƒã‚§ãƒƒã‚¯ã‚’ã—ãªã„
alias cp='nocorrect cp'			#ã‚¹ãƒšãƒ«ãƒã‚§ãƒƒã‚¯ã‚’ã—ãªã„
alias mkdir='nocorrect mkdir'	#ã‚¹ãƒšãƒ«ãƒã‚§ãƒƒã‚¯ã‚’ã—ãªã„
alias sudo='nocorrect sudo'	#ã‚¹ãƒšãƒ«ãƒã‚§ãƒƒã‚¯ã‚’ã—ãªã„
alias ssh-config="zed ~/.ssh/config"
alias sc="sudo systemctl"
alias E="emacsclient -a '' -t"
alias -g H='| head'
alias -g T='| tail'
alias -g G='| egrep --color=auto'
alias -g W='| wc'
alias -g P='| peco'
alias -g NL='>/dev/null'
alias -g NLL='&>/dev/null'
alias -g L="| less"
export PAGER=less

#ç®¡ç†è€…æ¨©é™ã§æ›¸ãè¾¼ã¿: /proc, /sys çµŒç”±ã®è¨­å®šç”¨
function suwrite { echo "$@[-1]" | sudo tee "${@:1:-1}" }

if [ $SYSTEM = 'Linux' ]; then
    netstat_tcp_opts="--tcp"
else
    netstat_tcp_opts="-ptcp"
fi

alias ts="netstat -a $netstat_tcp_opts"
function tf() { netstat $netstat_tcp_opts "$@"  | egrep -v '(::1|127.0.0.1|localhost)[:.].*(::1|127.0.0.1|localhost)[:.]' }

function p() { if [ \! -z "$1" ]; then
    if pids=$(pgrep -f "$@[-1]"); then
	ps "$@[1,-2]" -p $(echo $pids); else echo "No process found."; fi;
    else ps ax; fi }
alias p="nocorrect p"

if [ $SYSTEM = Cygwin ]; then
	alias ifconfig='ipconfig | iconv -f Shift_JIS -t UTF-8'
fi

###
# zshç’°å¢ƒè¨­å®š
###

#å±¥æ­´
HISTFILE=$HOME/.zsh-history
HISTSIZE=1000000
SAVEHIST=1000000
setopt hist_ignore_dups
setopt share_history
setopt extended_history
setopt HIST_IGNORE_SPACE
setopt HIST_REDUCE_BLANKS
function history-all { history -E 1 }

#ã‚­ãƒ¼ãƒã‚¤ãƒ³ãƒ‰
bindkey "[3~" delete-char
bindkey "[3D" backward-word
bindkey "[3C" forward-word
bindkey "[1;3D" backward-word
bindkey "[1;3C" forward-word
bindkey "[5D" backward-word
bindkey "[5C" forward-word
bindkey "[1;5D" backward-word
bindkey "[1;5C" forward-word
if zle -la | grep -q '^history-incremental-pattern-search'; then
  # zsh 4.3.10 ä»¥é™ã§ã®ã¿æœ‰åŠ¹
  bindkey '^R' history-incremental-pattern-search-backward
  bindkey '^S' history-incremental-pattern-search-forward
fi

autoload history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^p" history-beginning-search-backward-end
bindkey "^n" history-beginning-search-forward-end

#è£œå®Œ
zstyle ':completion:*:default' menu select=1	#tabäºŒå›ã§å€™è£œã‚’çŸ¢å°ã‚­ãƒ¼ã§é¸æŠ
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'	#å°æ–‡å­—ã¯å¤§æ–‡å­—ã«ã‚‚ä¸€è‡´
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}	#è£œå®Œå€™è£œã«è‰²ã‚’ä»˜ã‘ã‚‹
setopt completeinword	#å˜èªã®é€”ä¸­ã«ã‚«ãƒ¼ã‚½ãƒ«ã‚’ãŠã„ã¦è£œå®Œã™ã‚‹
setopt magic_equal_subst	#=ä»¥é™ã§ã‚‚è£œå®Œ
autoload -U compinit
compinit -u
setopt extended_glob
setopt correctall
CORRECT_IGNORE='_*'
CORRECT_IGNORE_FILE='.*'
setopt no_beep
setopt auto_param_slash
setopt list_packed		# è£œå®Œå€™è£œã‚’è©°ã‚ã¦è¡¨ç¤º
setopt list_types		# è£œå®Œå€™è£œã®ç¨®é¡ã‚’è¡¨ç¤º
setopt auto_cd			# ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªåã‚’å®Ÿè¡Œã™ã‚‹ã¨cd, ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªåã‚‚è£œå®Œã•ã‚Œã‚‹ã‚ˆã†ã«
setopt auto_pushd		# cd -[tabã‚­ãƒ¼]ã§éå»ã®ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã‚’è£œå®Œ
setopt auto_param_keys	# æ‹¬å¼§ç­‰ã®è£œå®Œ

hosts=( ${(@)${${(M)${(s:# :)${(zj:# :)${(Lf)"$([[ -f ~/.ssh/config ]] && < ~/.ssh/config)"}%%\#*}}##host(|name) *}#host(|name) }/\*} )
zstyle ':completion:*:hosts' hosts $hosts      # .ssh/configã«æŒ‡å®šã—ãŸãƒ›ã‚¹ãƒˆã‚’sshãªã©ã®è£œå®Œå€™è£œã«

#ãƒ—ãƒ­ãƒ³ãƒ—ãƒˆ
unsetopt promptcr		# æ”¹è¡Œã®ãªã„å‡ºåŠ›ã‚’ãƒ—ãƒ­ãƒ³ãƒ—ãƒˆã§ä¸Šæ›¸ãã™ã‚‹ã®ã‚’é˜²ã
setopt PROMPT_SUBST
PROMPT="%F{green}[%m-%T]%f%# "
RPROMPT="%(?..%F{red}-%?-)%F{green}[%1(v|%F{yellow}%1v%F{green} |)%n:%~]%f"

#è‡ªå‹•ãƒ­ã‚°ã‚¢ã‚¦ãƒˆ
autoload colors && colors
zmodload zsh/datetime # $EPOCHSECONDS, strftimeç­‰
reset_autologout() { IDLETIME=$EPOCHSECONDS }
check_autologout() {
    if [ "$AUTOLOGOUT" -gt 0 -a $[EPOCHSECONDS-IDLETIME] -ge "$AUTOLOGOUT" ]; then
        print "\n$fg[red]*** Auto logout ***$reset_color"
	kill -HUP $$
    fi
}
#ãƒ—ãƒ­ãƒ³ãƒ—ãƒˆã®æ™‚åˆ»ã‚’æ›´æ–°
reset_tmout() { TMOUT=$[60-EPOCHSECONDS%60] }
precmd_functions=($precmd_functions reset_tmout reset_autologout)
redraw_tmout() { zle reset-prompt; reset_tmout }
TRAPALRM() { check_autologout; redraw_tmout }

#ã‚¦ã‚£ãƒ³ãƒ‰ã‚¦ã‚¿ã‚¤ãƒˆãƒ«
case "$TERM" in
    xterm*|screen*|kterm*)
	print_esc() { print -nr ${1//[^\ -~]/_} }  #unprintableãªæ–‡å­—ã‚’??ã«ç½®æ›
	precmd_wintitle() { print -n "\e]0;"; print_esc "[${PWD/~HOME/~}:$HOST]"; print -n "\a" }
	preexec_wintitle() { print -n "\e]0;"; print_esc "$1 [${PWD/~HOME/~}:$HOST]"; print -n "\a" }
	precmd_functions=($precmd_functions precmd_wintitle)
	preexec_functions=($preexec_functions preexec_wintitle)
	;;
esac

#ãã®ä»–
autoload -U url-quote-magic  && zle -N self-insert url-quote-magic	#URLã‚’è²¼ä»˜æ™‚ã«è‡ªå‹•çš„ã«ã‚¨ã‚¹ã‚±ãƒ¼ãƒ—

autoload zmv				#ã¾ã¨ã‚ã¦ãƒªãƒãƒ¼ãƒ ç”¨
alias zmv='noglob zmv'	# zmv -W *.htm *.html ãªã©

autoload zed			# ç°¡æ˜“ã‚¨ãƒ‡ã‚£ã‚¿
autoload zargs			# zargs command

autoload -U zcalc
zmodload -i zsh/mathfunc	# æ•°å€¤æ¼”ç®—
setopt pushd_ignore_dups
setopt no_tify
setopt PRINT_EIGHT_BIT
setopt noflowcontrol
setopt numeric_glob_sort								#æ•°å€¤é †ã§ã‚½ãƒ¼ãƒˆ
export WORDCHARS='*?_-.[]~=&;!#$%^(){}<>'	#å˜èªã®åŒºåˆ‡ã‚Š

#cshäº’æ›
function setenv () {
	if [ $# -eq 0 ]; then
		/usr/bin/env
	else
		export $1=$*[2,-1]
	fi
}
trap "" USR1

#TCP file transfer
function file_recv {
	if [ $# = 0 ]; then
		echo file_recv listen_port
		return
	fi
	(
	autoload -U tcp_open; tcp_open >/dev/null 2>/dev/null # define ztcp
	ztcp -l "$1"
	fd_listen=$REPLY
	echo "Waiting on port $1 (fd $fd_listen) ..."
	ztcp -a $fd_listen || echo failed.
	fd_accept=$REPLY
	echo "Connected. (fd $fd_accept)"
	tar vxf - <&$fd_accept
	ztcp -c $fd_listen
	ztcp -c $fd_accept
	)
}
function file_send {
	if [ $# -lt 3 ]; then
		echo file_send host port files ...
		return
	fi
	(
	autoload -U tcp_open; tcp_open >/dev/null 2>/dev/null # define ztcp
	ztcp "$1" "$2"
	shift 2
	tar vcf - "$@" >&$REPLY
	ztcp -c $REPLY
	)
}

###### git é–¢é€£ ######

#gitãƒ–ãƒ©ãƒ³ãƒåè¡¨ç¤º
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:git:*' formats '%c%u%b'
zstyle ':vcs_info:git:*' actionformats '%c%u%b|%a'

#ã‚«ãƒ¬ãƒ³ãƒˆãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒª/ã‚³ãƒãƒ³ãƒ‰è¨˜éŒ²
local _cmd=''
local _lastdir=''
preexec_gitupdate() {
  _cmd="$1"
  _lastdir="$PWD"
}
preexec_functions=($preexec_functions preexec_gitupdate)
#gitæƒ…å ±æ›´æ–°
update_vcs_info() {
  psvar=()
  LANG=en_US.UTF-8 vcs_info
  [[ -n "$vcs_info_msg_0_" ]] && psvar[1]="$vcs_info_msg_0_"
}
#ã‚«ãƒ¬ãƒ³ãƒˆãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªå¤‰æ›´æ™‚/gité–¢é€£ã‚³ãƒãƒ³ãƒ‰å®Ÿè¡Œæ™‚ã«æƒ…å ±æ›´æ–°
precmd_gitupdate() {
  _r=$?
  case "${_cmd}" in
    git*|stg*) update_vcs_info ;;
    *) [ "${_lastdir}" != "$PWD" ] && update_vcs_info ;;
  esac
  return $_r
}
precmd_functions=($precmd_functions precmd_gitupdate)

############

# dotfilesã®è‡ªå‹•ã‚¢ãƒƒãƒ—ãƒ‡ãƒ¼ãƒˆ(1æ—¥1å›)
(: ~/.dotfiles/.git/FETCH_HEAD(md-1)) NLL || (cd ~/.dotfiles; [ "$(git pull)" = "Already up-to-date." ] || echo .dotfiles updated)&!

# .zshrc_by_hostã§å®šç¾©ã—ãŸå¾Œå‡¦ç†ã®å®Ÿè¡Œ
if declare -f _postinit_by_host >/dev/null; then
  _postinit_by_host
  unset -f _postinit_by_host
fi

